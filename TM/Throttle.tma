PARTITION
# PARTITION 15: Throttle Valve
#---------------------------------------------------------------
# Summary:
#   Throttle_Valve_Startup - Does nothing
#   Throttle_Valve_Init - Initializes valve, waits for RECORD
#   Throttle_Valve_Close - Drives to in limit or complains
#   Throttle_Valve_Closed - Where we go after closing valve.
#   Throttle_Valve_Failure - Reports in limit failure and sits
#   Throttle_Valve_PI_Init - How we get to Throttle_Valve_PI
#   Throttle_Valve_PI - Diff Pressure control algo
#   Throttle_Valve_Scan - Perform a scan, then go back to PI
#---------------------------------------------------------------

%{
  double DP_sum;
  // double TV_Gp = 16.0;
  // double TV_Gi = 4.0;
  // double TV_DP_Set = 2.25;
  int scan_done = 0;
  double TVPos0;
  uint16_t TVStep0;
  int dP_PI_Dir = 1;

  #define MAX_TV_STEPS 4000
  #include <math.h> /* for fabs */
%}

State Throttle_Valve_Idle {}

#---------------------------------------------------------------
# Initialization is triggered by the mainline, since we may
# want to wait for something.
#---------------------------------------------------------------
State Throttle_Valve_Init {
# +1 > Set Throttle Valve Speed 1067 Hz
  +1 > Set Inlet DP Gain Gp 16.0
  +1 > Set Inlet DP Gain Gi 4.0
  +1 > Set Inlet DP SetPoint 2.25
# +3 > Drive Throttle Valve in 8000
#    Hold Until (TVStat & 1) or 5
#    else { msg(2, "Throttle Valve In Limit not observed on init"); }
# +2 > Drive Throttle Valve out 0
# +2 > Drive Throttle Valve out 2100
# +2 > Drive Throttle Valve To 2100


  +5 { double Step;
       uint16_t IStep;
       TVPos0 = convert(TVPos);
       Step = (TVPos0-0.493149)/0.000591;
       if (Step > 10000) IStep = 10000;
       else if (Step < 0) TVStep0 = 0;
       else TVStep0 = Step;
       ci_sendfcmd(0, "Preset Throttle Valve Position To %u\n",
         TVStep0);
       Validate Throttle_Valve_Test_Drive;
     }
}

State Throttle_Valve_Test_Drive {
   +5 > Drive Throttle Valve out 200
   +1 { double dPos;
        dPos = convert(TVPos) - TVPos0;
        if (dPos > 0.0782 && dPos < 0.1582) {
          msg(0, "Throttle initial drive of 200 steps produced "
                "pot change of %.4lf V: In bounds", dPos);
          dP_PI_Dir = 1;
          Validate Throttle_Init_OK;
        } else if (dPos < -0.0782 && dPos > -0.1582) {
          msg(2, "Throttle initial drive of 200 steps produced "
                "pot change of %.4lf V: Direction inverted", dPos);
          dP_PI_Dir = -1;
          Validate Throttle_Init_OK;
        } else {
          msg(2, "Throttle initial drive of 200 steps produced "
                "pot change of %.4lf V: Out of bounds", dPos);
          dP_PI_Dir = 0;
          Validate Throttle_Not_Driving_Properly;
        }
      }
}

State Throttle_Init_OK {
   +1 > Drive Throttle Valve in 200
      Validate Throttle_Valve_Ready;
}

State Throttle_Not_Driving_Properly {}

State Throttle_Valve_Ready {
  depending on (RECORD) { Validate Throttle_Valve_PI_Init; }
}

State Throttle_Valve_Close {
  depending on (Ops_Shutdown) { Validate Throttle_Valve_Shutdown; }
  +1 > Drive Throttle Valve in 8000
     Hold Until ( TVStat & 1 ) or 5
     else Validate Throttle_Valve_Failure;
     > Drive Throttle Valve out 0
     Hold Until ( TVStep == 0 ) or 2
     else { msg( 2, "Throttle Valve step did not reset" ); }
     > Preset Throttle Valve Position to 0
     Validate Throttle_Valve_Closed;
}

State Throttle_Valve_Closed {
  depending on (Ops_Shutdown) { Validate Throttle_Valve_Shutdown; }
  depending on (RECORD) {
    Validate Throttle_Valve_PI_Init;
  }
}

#---------------------------------------------------------------
# In limit not observed: Retry periodically
#---------------------------------------------------------------
State Throttle_Valve_Failure {
  depending on (Ops_Shutdown) { Validate Throttle_Valve_Shutdown; }

  { msg( 2, "Throttle Valve did not close" ); }
  +1:00 Validate Throttle_Valve_Close;
}

State Throttle_Valve_PI_Init {
    { DP_sum = 2100; }
    Validate Throttle_Valve_PI;
}

State Throttle_Valve_PI NoLog {
  depending on (Ops_Shutdown) { Validate Throttle_Valve_Shutdown; }
  { double DP, Gp, Gi, DP_diff, DP_set, dstep;
    unsigned short ustep;
    DP = convert(InlDP);
    DP_set = convert(TV_DP_Set);
    Gp = convert(TV_Gp);
    Gi = convert(TV_Gi);
    DP_diff = DP - DP_set;
    DP_sum += DP_diff * Gi;
    if ( DP_sum > MAX_TV_STEPS )
      DP_sum = MAX_TV_STEPS;
    else if ( DP_sum < -MAX_TV_STEPS )
      DP_sum = -MAX_PINCH_STEPS/Gi;

    dstep = DP_diff * Gp + DP_sum;
    dstep = TVStep0 + dP_PI_Dir * (dstep - TVStep0);
    if (dstep<0) dstep = 0;
    else if (dstep > MAX_TV_STEPS) dstep = MAX_TV_STEPS;
    ustep = US_CAST(dstep);
    ci_sendfcmd(2, "Drive Throttle Valve To %u\n", ustep);
  }

  Hold Until ( TVStat & 1 );
  { msg(0, "Saw TV in limit: Make sure it comes off"); }
  Hold Until ( ! (TVStat & 1) ) or 30
  else Validate Throttle_Valve_Close;
  Validate Throttle_Valve_PI;
}

State Throttle_Valve_Scan {
    > Drive Throttle Valve In 8000
    Hold Until ( TVStat & 1 ) or 6;
    # else Validate Throttle_Valve_Close;
 +5 > Scan Throttle Valve Out 4000 By 40
    Hold Until ( TVScan != 0 ) or 1;
    Hold Until ( TVScan == 0 ) or 30;
    > Stop Throttle Valve
    Hold Until ( TVScan == 0 ) or 3; 
    > Drive Throttle Valve To 2100
 +2 Validate Throttle_Valve_PI_Init;
}

State Throttle_Valve_Shutdown {}
